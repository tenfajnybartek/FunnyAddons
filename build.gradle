plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'pl.tenfajnybartek'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name 'reposilite-snapshots-repository'
        url 'https://maven.reposilite.com/snapshots'
    }
    maven { url "https://repo.panda-lang.org/releases" }
    maven { url = "https://storehouse.okaeri.eu/repository/maven-public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly ('net.dzikoysk.funnyguilds:plugin:4.13.1-SNAPSHOT:all') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.sk89q.worldguard'
        exclude group: 'net.milkbowl.vault'
        exclude group: 'me.clip'
        exclude group: 'com.gmail.filoghost.holographicdisplays'
        exclude group: 'com.github.decentsoftware-eu'
        exclude group: 'us.dynmap'
    }
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'mysql:mysql-connector-java:8.0.33'
    compileOnly files('libs/Vault.jar')
    compileOnly files('libs/worldGuard.jar')
    compileOnly files('libs/PlaceholderAPI.jar')
    compileOnly files('libs/DecentHolograms.jar')
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
shadowJar {
    archiveClassifier.set('')
    relocate 'com.zaxxer.hikari', 'pl.tenfajnybartek.libs.hikari'
    relocate 'com.mysql', 'pl.tenfajnybartek.libs.mysql'
    minimize()
}
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
tasks.build.dependsOn tasks.shadowJar
